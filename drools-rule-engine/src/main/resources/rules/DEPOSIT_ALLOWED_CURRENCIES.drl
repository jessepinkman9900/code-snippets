package com.code_snippets.drools_rule_engine.rules

import com.code_snippets.drools_rule_engine.models.Transaction
global com.code_snippets.drools_rule_engine.models.ValidationResult validationResult

// only txn type deposit is allowed
// salience is used to control priority of rules
rule "Allow only deposit transactions" salience 10
    when
        $t: Transaction(type != "deposit")
    then
        validationResult.valid = false;
        validationResult.message = "Transaction type " + $t.getType() + " is not supported";
end

// List of allowed currencies
rule "Allow only supported currencies"
    when
        $t: Transaction(currency == null || currency == "" || currency != "USD")
    then
        validationResult.valid = false;
        validationResult.message = "Currency " + $t.getCurrency() + " is not supported";
end

// Validate transaction amount is positive
rule "Amount must be positive"
    when
        $t: Transaction(amount <= 0)
    then
        validationResult.valid = false;
        validationResult.message = "Transaction amount must be greater than zero";
end

// Validate required fields
rule "Transaction must have all required fields"
    when
        $t: Transaction(
            id == null || id.trim().isEmpty() ||
            type == null || type.trim().isEmpty() ||
            currency == null || currency.trim().isEmpty()
        )
    then
        validationResult.valid = false;
        validationResult.message = "Transaction is missing required fields";
end

// High-value transaction check
rule "High value transaction requires review"
    when
        $t: Transaction(amount > 10000)
    then
        validationResult.valid = false;
        validationResult.message = "High value transaction requires manual review";
end
